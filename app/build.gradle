import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {

    signingConfigs {
        release {
            keyAlias 'fine'
            keyPassword 'yexiaokang'
            storeFile file('keystore.jks')
            storePassword 'yexiaokang'
        }
    }

    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultConfig {
        applicationId "com.example.fine.ndksample"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs 'src/main/libs'
            jni.srcDirs = []                    // disable automatic ndk-build call
        }
    }

    // call regular ndk-build(.cmd) script
    task ndkBuild(type: Exec) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            def command = properties.getProperty('ndk.dir', null) + "/ndk-build.cmd"
            commandLine command, '-C', file('src/main/jni').absolutePath
        } else {
            def command = properties.getProperty('ndk.dir', null) + "/ndk-build"
            commandLine command, '-C', file('src/main/jni').absolutePath
        }
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.yxkang.android:support:0.0.7'
}
